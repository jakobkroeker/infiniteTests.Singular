/////////////////////
version="version test.intersect";
category="Commutative Algebra";
info="
";

LIB "primdecint.lib";
LIB "randomideal.lib";
LIB "ringutils.lib";
LIB "wrappers.lib";

proc intersect_via_eliminate(ideal i, ideal j) 
"USAGE:   intersect_via_eliminate(i,j);
@*        i,j = ideals
ASSUME:   i,j = ideals over the integers or a field
RETURN:   ideal, the intersection of i and j
EXAMPLE:  example intersect_via_eliminate;  shows an example" 
{
  ASSUME( 0 , isCommutative( ) );

  ASSUME( 0 , hasGlobalOrdering( basering ) );  

  if ( baseringIsExtensionField()   ||  
         not (  baseringHasIntegerCoefficientRing()  ||
                baseringHasRationalCoefficientField() ||
                baseringHasPrimeCoefficientField()
           ) 
      )
  { 
      ERROR("intersect_via_eliminate does not support given ring type");
  }
  
  if ( isZero(i) || isZero(j) )
  {
      return (ideal(0));
  }
  
  i = stdWrapper(i);
  j = stdWrapper(j);
  
 

  int bIsQring =   isQuotientRing( basering );
  ideal q = ideal(basering);

  def BAS = basering;

  // add one new variable "t" to a copy of basering
  int numVariables = nvars(basering);  
  int pos_t = numVariables + 1; // position of new variable 't' is numVariables + 1

  // introduce new variable called "t" 
  // hardcoded, but does not hurt, since there are no parameters. and 'x' is not in conflict with 't'
  string tempstr = "ring pr2=(" + charstr(BAS) + "),(x(1.."+  string(numVariables) + "), t), dp;"; 
  //ol.writeX( "tempstr", tempstr);
  execute(tempstr);   
  
  if ( not bIsQring )
  {
     def r2 = pr2;
  }
  else 
  {
     ideal   q = std(imap(BAS,q));
     qring r2 = q;
  }

  /*
  list rl = ringlist(basering);

  rl[2] =  indexedVariables("x", numVariables) + list("t");;
  dbprint(printlevel-voice, "rl[2] = ", rl[2] );
  // change variable ordering to dp:
  rl =  changeRingListVarOrderTo_dp (rl);
  dbprint(printlevel-voice, "rl[2] = ", rl[2] );

  // difference to previous variant: if basering is a qring, then r2 is also a qring.
  def r2 = ring(rl);
  setring r2;
  // */
  //dbprint(printlevel-voice, "intersect_via_eliminate:: r2 = ", basering );
  //dbprint(printlevel-voice, "intersect_via_eliminate:: var(pos_t)= ", var(pos_t) );
  

  // for i=(f_1,...,f_s) and j=(g_1,...,g_t) compute the ideal
  // J=(tf_1,...,tf_s,(1-t)g_1,...,(1-t)g_t)
  // then we eliminate "t" from J and map the new J to our basering
  ideal J = 0;

  int k;
  ideal ii = fetch(BAS,i);
  ii = stdWrapper(ii); //not necessary due to Anne
  for(k=1; k <= nGens(ii); k++) 
  {
     J = J,var(pos_t)*ii[k];
  }
  ideal jj = fetch(BAS,j);
  jj = stdWrapper(jj); //not necessary due to Anne
  for(k=1; k <= nGens(jj); k++)
  {
     J = J,(1 - var(pos_t) )*jj[k];
  }

  ideal e = eliminate(J, var(pos_t) );
  setring BAS;

  ideal result1 = fetch(r2,e); 

  for(k=1; k <= nGens(result1); k++)
  {
     //ASSUME(0, (leadcoef(result1[k]) >0));
     if(leadcoef(result1[k]) < 0)
     {
        //ERROR("intersect_via_eliminate::Warning: negative leadcoef occured");
        result1[k] = -result1[k];
     }
  }
  return(result1);
}


proc testIntersect( getRng, idealG, trials, testflags, optionlist, ol )
{
    int warnIsSet = optionIsSet("warn");
    option(nowarn);
    if (defined(basering) ) {   def BAS=basering; }
      def rng = getRng( );
      setring rng;
      option(warn);

      
    short = 0;
    int trial;

    ideal cs,  I, J, Q;
    ring qrng;
    
    
    while( trial<trials )
    {   
      option(nowarn);  
      kill rng;      
      def rng = getRng( );
      setring rng;
      ideal cs,  I, J, gI, gJ, ggI, ggJ, I_J, gI_gJ, e_I_J, e_gI_gJ, z_I_J;


      if (testflags.optionflagTest)
      {
        if ( hasGlobalOrLocalOrdering(basering))
        {
           chooseRandomOptionsFromList(optionlist);
        }
      }
      if (warnIsSet)       {         option(warn);      }
      
       ol.clearX();
       ol.writeX( "");
       ol.writeX( "");

       ol.writeX( "LIB \"wrappers.lib\";");
       ol.writeX( "LIB \"src/randomRing.lib\" ;");
       ol.writeX( "LIB(\"src/tests/test.intersect\");");
       
      
       ol.writeX(  ringToStringHack (basering) );

       if (defined(qrng)) {kill qrng; }
       
       ol.writeX( "// int trial= "+string( trial)+";" );
       I = idealG( );
       J = idealG( );
       ol.writeX( "system(\"random\", " + string( system("random") ) + ");" );
       ol.writeX( "ideal I = "+string( I)+";" );
       ol.writeX( "ideal J = "+string( J)+";" );
       ol.writeX( "ideal gI = std(I);" );
       ol.writeX( "ideal gJ = std(J);" );       
       gI = std(I);
       gJ = std(J);

       ol.writeX( "ideal I_J =  intersect(I,J);" );
       I_J =  intersect(I,J);
       ol.writeX( "ASSUME(0, isSubModule(I_J, I));" );
       ol.writeX( "ASSUME(0, isSubModule(I_J, J));" );
       ASSUME(0, isSubModule(I_J, I));
       ASSUME(0, isSubModule(I_J, J));


       ol.writeX( "ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,I_J)   ) );" );
       ol.writeX( "ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,  J)   ) );" );
       ol.writeX( "ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,  gI)   ) );" );
       ol.writeX( "ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,  I+gJ)   ) );" );
       ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,I_J)   ) );
       ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,J)   ) );
       ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,gI)   ) );
       ASSUME(0, idealsAreEqual(I_J     , intersect(I_J,I+gJ)   ) );


       if ((char(rng)==0) && (not (isQuotientRing(basering)) ))
       {
           ol.writeX( "ideal z_I_J =  intersectZ(I,J);" );
           z_I_J = intersectZ(I,J);
           ol.writeX( "  ASSUME(0, idealsAreEqual(I_J    , z_I_J    ) );");
           ASSUME(0, idealsAreEqual(I_J     , z_I_J    ) );
       }
       ol.writeX( " ideal gI_gJ = intersect(gI,gJ);" );
       gI_gJ = intersect(gI,gJ);
       ol.writeX( "  ASSUME(0, idealsAreEqual(I_J    , gI_gJ    ) );");
       ASSUME(0, idealsAreEqual(I_J     , gI_gJ    ) );

       if (hasGlobalOrdering(basering)  )
       {
           ol.writeX( " ideal e_I_J =  intersect_via_eliminate(I,J);"  );
           e_I_J =  intersect_via_eliminate(I,J);
           ol.writeX( " e_gI_gJ =  intersect_via_eliminate( gI, gJ);"  );
           e_gI_gJ =  intersect_via_eliminate( gI, gJ);
           ol.writeX( "  ASSUME(0, idealsAreEqual(I_J    , e_I_J    ) );");     
           ol.writeX( "  ASSUME(0, idealsAreEqual(gI_gJ  ,  e_gI_gJ ) );");

           ASSUME(0, idealsAreEqual(I_J     , e_I_J    ) );
           
           ASSUME(0, idealsAreEqual(gI_gJ   , e_gI_gJ  ) );
       }
       trial = trial+1;
    }

    if ( defined(BAS) ) {  setring BAS; }
    return(1);
}


 
	


