

option("redSB");

echo = 0;
LIB("primdec.lib");
LIB("ehv.lib");
LIB("randomIdeal.lib");
 
proc zeroRad_orig(ideal I,list #)
"USAGE:  zeroRad(I) , I a zero-dimensional ideal
 RETURN: the radical of I
 NOTE:  Algorithm of Kemper
 EXAMPLE: example zeroRad; shows an example"
{
   if(homog(I)==1){return(maxideal(1));}
   //I needs to be a reduced standard basis
   def R=basering;
   int m=npars(R);
   int n=nvars(R);
   int p=char(R);
   int d=vdim(I);
   int i,k;
   list l;
   if(((p==0)||(p>d))&&(d==deg(I[1])))
   {
     intvec e=leadexp(I[1]);
     for(i=1;i<=nvars(basering);i++)
     {
       if(e[i]!=0) break;
     }
     I[1]=sep(I[1],i)[1];
     return(interred(I));
   }
   intvec op=option(get);

   option(redSB);
   ideal F=finduni(I);//F[i] generates I intersected with K[var(i)]

   option(set,op);
   if(size(#)>0){I=#[1];}

   for(i=1;i<=n;i++)
   {
      l[i]=sep(F[i],i);
      F[i]=l[i][1];
      if(l[i][2]>k){k=l[i][2];}  //computation of the maximal k
   }

   if((k==0)||(m==0)) //the separable case
   {
    intvec save=option(get);option(redSB);
    I=interred(I+F);option(set,save);return(I);
   }
   //I=simplify(I,1);

   for(i=1;i<=n;i++)             //consider all polynomials over
   {                             //Fp(t(1)^(p^-k),...,t(m)^(p^-k))
      F[i]=powerCoeffs(F[i],k-l[i][2]);
   }

   string cR="ring @R="+string(p)+",("+parstr(R)+","+varstr(R)+"),dp;";
   execute(cR);
   ideal F=imap(R,F);

   string nR1="ring @S1="+string(p)+",("+varstr(R)+","+parstr(R)+",@y(1..m)),dp;";
   execute(nR1);
   list lR=ringlist(@S1)[2];
   lR=lR[(size(lR)-m+1)..(size(lR))];

   string nR="ring @S="+string(p)+",("+string(lR)+","+varstr(R)+","+parstr(R)+"),dp;";
   execute(nR);

   ideal G=fetch(@R,F);    //G[i](t(1)^(p^-k),...,t(m)^(p^-k),x(i))=sep(F[i])

   ideal I=imap(R,I);
   ideal J=I+G;
   poly el=1;
   k=p^k;
   for(i=1;i<=m;i++)
   {
      J=J,var(i)^k-var(m+n+i);
      el=el*var(i);
   }

   J=eliminate(J,el);
   setring R;
   ideal J=imap(@S,J);
   return(J);
}


 proc zeroRad_new(ideal I,list #)
"USAGE:  zeroRad(I) , I a zero-dimensional ideal
 RETURN: the radical of I
 NOTE:  Algorithm of Kemper
 EXAMPLE: example zeroRad; shows an example"
{
   if(homog(I)==1){return(maxideal(1));}
   //I needs to be a reduced standard basis
   def R=basering;
   int m=npars(R);
   int n=nvars(R);
   int p=char(R);
   int d=vdim(I);
   int i,k;
   list l;
   if(((p==0)||(p>d))&&(d==deg(I[1])))
   {
     intvec e=leadexp(I[1]);
     for(i=1;i<=nvars(basering);i++)
     {
       if(e[i]!=0) break;
     }
     I[1]=sep(I[1],i)[1];
     return(interred(I));
   }
   intvec op=option(get);

   option(redSB);
   ideal F=finduni(I);//F[i] generates I intersected with K[var(i)]

   option(set,op);
   if(size(#)>0){I=#[1];}

   for(i=1;i<=n;i++)
   {
      l[i]=sep(F[i],i);
      F[i]=l[i][1];
      if(l[i][2]>k){k=l[i][2];}  //computation of the maximal k
   }

   if((k==0)||(m==0)) //the separable case
   {
    intvec save=option(get);option(redSB);
    I=interred(I+F);option(set,save);return(I);
   }
   //I=simplify(I,1);// considered harmful.

   for(i=1;i<=n;i++)             //consider all polynomials over
   {                             //Fp(t(1)^(p^-k),...,t(m)^(p^-k))
      F[i]=powerCoeffs(F[i],k-l[i][2]);
   }

   string cR="ring @R="+string(p)+",("+parstr(R)+","+varstr(R)+"),dp;";
   execute(cR);
   ideal F=imap(R,F);
   ideal I=imap(R,I);
   dbprint(printlevel-voice, "in @R, I=",I);
   dbprint(printlevel-voice, "in @R, F=",F);


   // 1. renaming: parstr->ppar, varstr->vvar   
   //string nR1="ring @S1="+string(p)+",( ppar(1.."+string(m)+"),vvar(1.."+string(n)+") ),dp;";
   //execute(nR1);
    
   //ideal I = fetch(@R,I);
   //dbprint(printlevel-voice, "in @S1, I=",I);

   // 2. introducing nev variables y(1..npars(R)). Due to renaming we control all variable names and there can be no conflicts.
   string nR="ring @S="+string(p)+",(y(1.."+string(m)+"),vvar(1.."+string(n)+"),ppar(1.."+string(m)+") ),dp;";
   execute(nR);

   ideal G=fetch(@R,F);    //G[i](t(1)^(p^-k),...,t(m)^(p^-k),x(i))=sep(F[i])
   //ideal I= imap(@S1,I);

   ideal  map@R@SIdeal ;
   int mpos;
   for (mpos  =    1; mpos <= m;    mpos++)   {    map@R@SIdeal[mpos]=var(m+n+mpos);  } // map parstr y to ppar
   for (mpos = m  +1; mpos <= m+n;  mpos++)   {    map@R@SIdeal[mpos]=var(mpos);    } // map varstr to vvar

   map map@R@S= @R,map@R@SIdeal;
   ideal I= map@R@S(I);

   dbprint(printlevel-voice, "in @S, I=",I);
   dbprint(printlevel-voice, "in @S, G=",G);

   ideal J=I+G;
   poly el=1;
   k=p^k;
   for(i=1;i<=m;i++)
   {
      J=J,var(i)^k-var(m+n+i);
      el=el*var(i);
   }

   J=eliminate(J,el);
   dbprint(printlevel-voice, "in @S, J=",J);

   //setring @S1;
   //ideal J=imap(@S,J);
   //dbprint(printlevel-voice, "in @S1, J=",J);
   //setring @R;
   //ideal J=fetch(@S1,J);
   //dbprint(printlevel-voice, "in @R, J=",J);
   setring R;
   ideal  mapIdeal ;
 
   for (mpos  =    1; mpos <= m;    mpos++)   {    mapIdeal[mpos]=0;              } // map y to 0
   for (mpos = m  +1; mpos <= m+n;  mpos++)   {    mapIdeal[mpos]=var(mpos-m);    } // map vvar to vars
   for (mpos = m+n+1; mpos <= m+n+m;mpos++)   {    mapIdeal[mpos]=par(mpos-m-n);  } // map ppar to pars

   map backmap= @S,mapIdeal;
   ideal J=backmap(J);
   //ideal J=imap(@R,J);

   dbprint(printlevel-voice, "in R, J=",J);
   return(J);
}


proc testZeroRad_patch(  pRng ,idealGen, trials )
{
    print ("testPatchedSplitCharp");
    if (defined(basering) ) {   def BAS=basering; }

    def zeroRadStr=string(Primdec::zeroRad);
    kill Primdec::zeroRad;
    
    setring pRng;

    int trial;
    list L1,L2;
    ideal I;
    for (trial = 1; trial<=trials; trial++)
    {   
       setring pRng;
       print ("trial: "+string(trial));
      
       I = idealGen();
       print ("I:");
       I;
       if (not idealsEqual(I,ideal(0)) && not idealsEqual(ideal(1),I ) )
       {
            if ( defined(zeroRad) ) { kill zeroRad; }
            proc zeroRad(list l) 
            {
               return (zeroRad_orig(l));
            } 
            export(zeroRad);
            L1 = primdecGTZ(I);
            if (defined(zeroRad)) { kill zeroRad; }
              proc zeroRad(list l) 
            {
               return (zeroRad_new(l));
            } 
            export(zeroRad);
            L2 = primdecGTZ(I);
            ASSUME(0, primDecsAreEquivalent(L1, L2) );

            testPrimary( L1, I );
       }
    }


    if (defined(BAS)) {  setring BAS; }
    return(1);
}


proc testZeroRad_patchWrapper(cchar,cord)
{
      init_randomIdeal();
      string cmd = "ring rng ="+cchar+" ,(x,y,z),"+cord+";";
      execute(cmd);
          
      def genParams = defaultIdealGeneratorParams();
      // adjust random ideal generator params:
      genParams.maxGens=4; 
      genParams.absMaxCoeff = 15;
      genParams.maxMonomialDegree = 3;
      genParams.maxTermsPerGen = 4;
      genParams.maxGens = 4;

      def idealG = idealGeneratorFromParams( genParams  );

      int trials = 3000;
       testZeroRad_patch( rng, idealG, trials);
}

testZeroRad_patchWrapper("0","dp");

testZeroRad_patchWrapper("7","dp");
testZeroRad_patchWrapper("7","lp"); 
testZeroRad_patchWrapper("7","Dp");
testZeroRad_patchWrapper("5","dp");
testZeroRad_patchWrapper("5","lp");
testZeroRad_patchWrapper("5","Dp");

testZeroRad_patchWrapper("19","dp");
testZeroRad_patchWrapper("19","lp");
testZeroRad_patchWrapper("19","Dp");



